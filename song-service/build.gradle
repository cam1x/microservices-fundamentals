plugins {
    id 'maven-publish'
    id "org.flywaydb.flyway"
    id "org.springframework.cloud.contract"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${netflixEurekaVersion}"

    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"

    runtimeOnly "org.postgresql:postgresql"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"

    implementation "org.projectlombok:lombok-mapstruct-binding:${gradleMapstructVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier:${springCloudContractVersion}"
}

contracts {
    testFramework = "JUNIT5"
    baseClassForTests = "com.chachotkin.song.service.controller.SongContractTestBase"
    contractsDslDir = new File("${projectDir}/src/contractTest/resources/contracts/")
}

contractTest {
    useJUnitPlatform()
    finalizedBy(publishToMavenLocal, consumerContractTest)
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    contractTest {
        java {
            srcDir '/src/contractTest/java'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
            artifact verifierStubsJar

            // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/273
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}

